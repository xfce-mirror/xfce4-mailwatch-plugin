dnl configure.ac.in
dnl
dnl xfce4-mailwatch-plugin - a mail notification plugin for the xfce4 panel
dnl

dnl version info
m4_define([mailwatch_version_major], [1])
m4_define([mailwatch_version_minor], [1])
m4_define([mailwatch_version_micro], [0])
m4_define([mailwatch_version_build], [@REVISION@])
m4_define([mailwatch_version_tag], [git])
m4_define([mailwatch_version], [mailwatch_version_major().mailwatch_version_minor().mailwatch_version_micro()ifelse(mailwatch_version_tag(), [git], [mailwatch_version_tag()-mailwatch_version_build()], [mailwatch_version_tag()])])

dnl init autoconf
AC_INIT([xfce4-mailwatch-plugin], [mailwatch_version],
        [http://bugzilla.xfce.org/])
AC_PREREQ([2.50])

dnl init automake
AM_INIT_AUTOMAKE([dist-bzip2])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE

dnl check for UNIX variants
AC_AIX
AC_ISC_POSIX
AC_MINIX
AM_CONDITIONAL([HAVE_CYGWIN], [test "`uname | grep \"CYGWIN\"`" != ""])

dnl check for basic programs
AC_PROG_CC
AM_PROG_CC_C_O
AC_PROG_INSTALL
AC_PROG_INTLTOOL([0.31], [no-xml])
LT_PREREQ([2.2.6])

dnl check for standard header files
AC_HEADER_STDC
AC_CHECK_HEADERS([errno.h fcntl.h math.h netdb.h netinet/in.h \
                  signal.h stddef.h string.h sys/select.h \
                  sys/socket.h sys/stat.h sys/types.h sys/wait.h \
                  time.h unistd.h])
AC_CHECK_FUNCS([mmap sigaction srandom])

dnl Check for i18n support
XDT_I18N([@LINGUAS@])

dnl Check for X11 installed
XDT_CHECK_LIBX11_REQUIRE
XDT_CHECK_LIBSM

dnl required
XDT_CHECK_PACKAGE([GMODULE], [gmodule-2.0], [2.4.0])
XDT_CHECK_PACKAGE([GOBJECT], [gobject-2.0], [2.4.0])
XDT_CHECK_PACKAGE([GTHREAD], [gthread-2.0], [2.4.0])
XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [2.6.0])
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.2.0])
XDT_CHECK_PACKAGE([LIBXFCEGUI4], [libxfcegui4-1.0], [4.2.0])
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-1.0], [4.3.20])

dnl see if we have a new enough libxfce4util for the posix signal handler stuff
saved_CFLAGS="$CFLAGS"
saved_LIBS="$LIBS"
CFLAGS="$CFLAGS $LIBXFCE4UTIL_CFLAGS"
LIBS="$LIBS $LIBXFCE4UTIL_LIBS"
AC_CHECK_FUNCS([xfce_posix_signal_handler_init])
CFLAGS="$saved_CFLAGS"
LIBS="$saved_LIBS"

dnl check for gnutls and libgcrypt support
XDT_CHECK_OPTIONAL_PACKAGE([GNUTLS], [gnutls], [1.2.0], [ssl],
    [gnutls and libgcrypt support for secure IMAP/POP3 connections], [yes])
if test "x$GNUTLS_FOUND" = "xyes"; then
    dnl libgcrypt does not provide a pkg-config file
    AM_PATH_LIBGCRYPT([1.2.0], LIBGCRYPT_FOUND=yes, LIBGCRYPT_FOUND=no)
    if test "x$LIBGCRYPT_FOUND" = "xyes"; then
        AC_DEFINE([HAVE_SSL_SUPPORT], [1], [Define if SSL support is enabled])
    fi
fi
AM_CONDITIONAL([HAVE_SSL_SUPPORT],
               [test "x$GNUTLS_FOUND" = "xyes" -a "x$LIBGCRYPT_FOUND" = "xyes"])

dnl to ipv6, or to not ipv6
AC_MSG_CHECKING([whether to support IPv6])
AC_ARG_ENABLE([ipv6],
              [AC_HELP_STRING([--disable-ipv6],
                              [Disable IPv6 support (default=enabled)])],
              [enable_ipv6_support=$enableval], [enable_ipv6_support=yes])
AC_MSG_RESULT([$enable_ipv6_support])
if test "x$enable_ipv6_support" = "xyes"; then
    AC_DEFINE([ENABLE_IPV6_SUPPORT], [1],
              [Define if IPv6 support should be enabled])
fi

dnl suppress warnings related to GStaticMutexes
XDT_SUPPORTED_FLAGS([CFLAGS], [-fno-strict-aliasing])

dnl check for debugging support
XDT_FEATURE_DEBUG

AC_DEFINE_UNQUOTED([WEBSITE],
                   ["http://spuriousinterrupt.org/projects/mailwatch"],
                   [The Mailwatch website])

AC_OUTPUT([
Makefile
doc/Makefile
doc/C/Makefile
doc/C/images/Makefile
doc/version
libmailwatch-core/Makefile
panel-plugin/Makefile
pixmaps/Makefile
po/Makefile.in
])

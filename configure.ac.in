dnl
dnl xfce4-mailwatch-plugin - a mail notification plugin for the xfce4 panel
dnl

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([mailwatch_version_major], [1])
m4_define([mailwatch_version_minor], [1])
m4_define([mailwatch_version_micro], [0])
m4_define([mailwatch_version_nano], []) dnl leave this empty to have no nano version
m4_define([mailwatch_version_build], [@REVISION@])
m4_define([mailwatch_version_tag], [git])
m4_define([mailwatch_version], [mailwatch_version_major().mailwatch_version_minor().mailwatch_version_micro()ifelse(mailwatch_version_nano(), [], [], [.mailwatch_version_nano()])ifelse(mailwatch_version_tag(), [git], [mailwatch_version_tag()-mailwatch_version_build()], [mailwatch_version_tag()])])

dnl ***************************
dnl *** Initialize autoconf ***
dnl ***************************
AC_INIT([xfce4-mailwatch-plugin], [mailwatch_version], [http://bugzilla.xfce.org/])
AC_PREREQ([2.50])
AC_REVISION([mailwatch_version_build])

dnl ***************************
dnl *** Initialize automake ***
dnl ***************************
AM_INIT_AUTOMAKE([1.8 no-dist-gzip dist-bzip2 tar-ustar])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE()
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

dnl **************************
dnl *** Initialize libtool ***
dnl **************************
LT_PREREQ([2.2.6])
LT_INIT([disable-static])

dnl *******************************
dnl *** Check for UNIX variants ***
dnl *******************************
AC_AIX()
AC_ISC_POSIX()

dnl ********************************
dnl *** Check for basic programs ***
dnl ********************************
AC_PROG_CC()
AM_PROG_CC_C_O()
AC_PROG_LD()
AC_PROG_INSTALL()
AC_PROG_INTLTOOL()

dnl **********************************
dnl *** Check for standard headers ***
dnl **********************************
AC_HEADER_STDC()
AC_CHECK_HEADERS([stdlib.h unistd.h locale.h stdio.h errno.h time.h string.h \
                  math.h sys/types.h sys/wait.h memory.h signal.h sys/prctl.h \
                  libintl.h fcntl.h netdb.h netinet/in.h stddef.h sys/select.h \
		  sys/socket.h sys/stat.h])
AC_CHECK_FUNCS([mmap sigaction srandom bind_textdomain_codeset])

dnl ******************************
dnl *** Check for i18n support ***
dnl ******************************
XDT_I18N([@LINGUAS@])

dnl *******************************
dnl *** Check for X11 installed ***
dnl *******************************
XDT_CHECK_LIBX11_REQUIRE()

dnl ***********************************
dnl *** Check for required packages ***
dnl ***********************************
XDT_CHECK_PACKAGE([GTHREAD], [gthread-2.0], [2.18.0])
XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [2.18.0])
XDT_CHECK_PACKAGE([LIBXFCE4UI], [libxfce4ui-1], [4.8.0])
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.8.0])
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-1.0], [4.8.0])

dnl Check for gnutls and libgcrypt support
XDT_CHECK_OPTIONAL_PACKAGE([GNUTLS], [gnutls], [1.2.0], [ssl],
    [gnutls and libgcrypt support for secure IMAP/POP3 connections], [yes])
if test "x$GNUTLS_FOUND" = "xyes"; then
    dnl libgcrypt does not provide a pkg-config file
    AM_PATH_LIBGCRYPT([1.2.0], LIBGCRYPT_FOUND=yes, LIBGCRYPT_FOUND=no)
    if test "x$LIBGCRYPT_FOUND" = "xyes"; then
        AC_DEFINE([HAVE_SSL_SUPPORT], [1], [Define if SSL support is enabled])
    fi
fi
AM_CONDITIONAL([HAVE_SSL_SUPPORT],
               [test "x$GNUTLS_FOUND" = "xyes" -a "x$LIBGCRYPT_FOUND" = "xyes"])

dnl Check for IPv6 support
AC_MSG_CHECKING([whether to support IPv6])
AC_ARG_ENABLE([ipv6],
              [AC_HELP_STRING([--disable-ipv6],
                              [Disable IPv6 support (default=enabled)])],
              [enable_ipv6_support=$enableval], [enable_ipv6_support=yes])
AC_MSG_RESULT([$enable_ipv6_support])
if test "x$enable_ipv6_support" = "xyes"; then
    AC_DEFINE([ENABLE_IPV6_SUPPORT], [1],
              [Define if IPv6 support should be enabled])
fi

dnl Suppress warnings related to GStaticMutexes
XDT_SUPPORTED_FLAGS([CFLAGS], [-fno-strict-aliasing])

dnl ***********************************
dnl *** Check for debugging support ***
dnl ***********************************
XDT_FEATURE_DEBUG()

dnl *********************************
dnl *** Substitute platform flags ***
dnl *********************************
AC_MSG_CHECKING([PLATFORM_CPPFLAGS])
AC_MSG_RESULT([$PLATFORM_CPPFLAGS])
AC_SUBST([PLATFORM_CPPFLAGS])
AC_MSG_CHECKING([PLATFORM_CFLAGS])
AC_MSG_RESULT([$PLATFORM_CFLAGS])
AC_SUBST([PLATFORM_CFLAGS])
AC_MSG_CHECKING([PLATFORM_LDFLAGS])
AC_MSG_RESULT([$PLATFORM_LDFLAGS])
AC_SUBST([PLATFORM_LDFLAGS])

AC_DEFINE_UNQUOTED([WEBSITE],
                   ["http://spuriousinterrupt.org/projects/mailwatch"],
                   [The Mailwatch website])

AC_OUTPUT([
Makefile
doc/Makefile
doc/C/Makefile
doc/C/images/Makefile
doc/version
icons/Makefile
icons/48x48/Makefile
icons/scalable/Makefile
libmailwatch-core/Makefile
panel-plugin/Makefile
po/Makefile.in
])

dnl ***************************
dnl *** Print configuration ***
dnl ***************************
echo
echo "Build Configuration:"
echo
echo "* Debug Support:    $enable_debug"
echo
